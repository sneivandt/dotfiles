#compdef dotfiles

typeset -A opt_args
setopt extendedglob

_dotfiles_opts_commands=(
  {-I,--install}"[Install]"
  {-T,--test}"[Test]"
  {-U,--uninstall}"[Uninstall]"

  {-h,--help}"[Display usage]"
)

_dotfiles_profiles=(
  "base:Minimal core shell configuration"
  "arch:Arch Linux headless"
  "arch-desktop:Arch Linux desktop"
  "windows:Windows environment"
)

_dotfiles_opts_common=(
  "-v[Enable verbose logging]"
)

_dotfiles_opts_install=(
  "--profile[Select profile]:profile:_describe 'profile' _dotfiles_profiles"
  "--dry-run[Preview changes without system modifications]"
  "--skip-os-detection[Skip automatic OS detection overrides]"
)

_dotfiles_action_install() {
  _arguments -s : \
    "(--install -I)"{-I,--install} \
    "$_dotfiles_opts_install[@]" \
    "$_dotfiles_opts_common[@]"
}

_dotfiles_action_test() {
  _arguments -s : \
    "(--test -T)"{-T,--test} \
    "-v[Enable verbose logging]"
}

_dotfiles_action_uninstall() {
  _arguments -s : \
    "(--uninstall -U)"{-U,--uninstall} \
    "$_dotfiles_opts_install[@]" \
    "$_dotfiles_opts_common[@]"
}

local -a args cmds;
local tmp
args=( ${${(M)words:#-*}#-} )
for tmp in $words
do
  cmds+=("${${_dotfiles_opts_commands[(r)*$tmp\[*]%%\[*}#*\)}")
done

case $args in
  h)
    return 0
    ;;
  I*)
    _dotfiles_action_install
    ;;
  T*)
    _dotfiles_action_test
    ;;
  U*)
    _dotfiles_action_uninstall
    ;;
  *)
    case ${(M)words:#--*} in
      *--help*)
        return 0
        ;;
      *--install*)
        _dotfiles_action_install
        ;;
      *--test*)
        _dotfiles_action_test
        ;;
      *--uninstall*)
        _dotfiles_action_uninstall
        ;;
      *)
        _arguments -s : \
          "$_dotfiles_opts_commands[@]"
        ;;
    esac
esac
