#!/usr/bin/env zsh

# Guard against multiple sourcing
[ -n "$ZSHRC_LOADED" ] && return
export ZSHRC_LOADED=1

# Quiet
setopt no_beep
setopt NO_LIST_BEEP
set bell-style none

# Word Style
autoload -Uz select-word-style
select-word-style bash

# Colors
autoload -Uz colors
colors

# History
setopt histignorealldups
setopt sharehistory
setopt appendhistory
setopt extendedglob
setopt hist_expire_dups_first
setopt hist_find_no_dups
setopt hist_verify
[ -d ~/.cache/zsh ] || mkdir -p ~/.cache/zsh
HISTFILE=~/.cache/zsh/history
HISTSIZE=10000
SAVEHIST=10000

# Various options
setopt autocd
setopt autopushd
setopt pushdminus
setopt pushdsilent
setopt pushdtohome

# Path
if [ -f ~/.config/shell/path.sh ]; then
  . ~/.config/shell/path.sh
else
  echo "Warning: ~/.config/shell/path.sh not found" >&2
fi

# Aliases
if [ -f ~/.config/shell/aliases.sh ]; then
  . ~/.config/shell/aliases.sh
else
  echo "Warning: ~/.config/shell/aliases.sh not found" >&2
fi

# GUI Aliases (optional)
[ -f ~/.config/shell/aliases-desktop.sh ] && . ~/.config/shell/aliases-desktop.sh

# Prompt
if [ -f ~/.config/zsh/prompt.zsh ]; then
  . ~/.config/zsh/prompt.zsh
else
  echo "Warning: ~/.config/zsh/prompt.zsh not found" >&2
fi

# Completion
if [ -f ~/.config/zsh/completion.zsh ]; then
  . ~/.config/zsh/completion.zsh
else
  echo "Warning: ~/.config/zsh/completion.zsh not found" >&2
fi

# Key bindings
if [ -f ~/.config/zsh/key-bindings.zsh ]; then
  . ~/.config/zsh/key-bindings.zsh
else
  echo "Warning: ~/.config/zsh/key-bindings.zsh not found" >&2
fi

# Plugins
for plugin in ~/.config/zsh/plugins/*.zsh(N)
do
  . "$plugin"
done

# Performance: Cache system plugin paths to avoid repeated filesystem checks
# Only search for plugins if cache is stale (older than 24 hours)
typeset -g _ZSH_PLUGIN_CACHE="${HOME}/.cache/zsh/system-plugins.cache"
typeset -g _ZSH_PLUGIN_LIST=()

if [[ -f "$_ZSH_PLUGIN_CACHE" && -n ${_ZSH_PLUGIN_CACHE}(#qNmh-24) ]]; then
  # Cache is fresh, load from cache
  source "$_ZSH_PLUGIN_CACHE"
else
  # Build cache
  typeset -ga _ZSH_PLUGIN_CANDIDATES=(
    /opt/homebrew/share/zsh-autosuggestions/zsh-autosuggestions.zsh
    /opt/homebrew/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
    /usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh
    /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
    /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
    /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
  )

  _ZSH_PLUGIN_LIST=()
  for plugin in "${_ZSH_PLUGIN_CANDIDATES[@]}"; do
    if [[ -f "$plugin" ]]; then
      _ZSH_PLUGIN_LIST+=("$plugin")
    fi
  done

  # Save cache
  mkdir -p ~/.cache/zsh
  echo "typeset -ga _ZSH_PLUGIN_LIST=(" > "$_ZSH_PLUGIN_CACHE"
  for plugin in "${_ZSH_PLUGIN_LIST[@]}"; do
    echo "  \"$plugin\"" >> "$_ZSH_PLUGIN_CACHE"
  done
  echo ")" >> "$_ZSH_PLUGIN_CACHE"
fi

# Load cached plugins
for plugin in "${_ZSH_PLUGIN_LIST[@]}"; do
  source "$plugin"
done

# zoxide
if command -v zoxide >/dev/null 2>&1
then
  eval "$(zoxide init zsh)"
fi

# FZF (optional)
[ -f /usr/share/fzf/key-bindings.zsh ] && . /usr/share/fzf/key-bindings.zsh
[ -f /usr/share/fzf/completion.zsh ] && . /usr/share/fzf/completion.zsh

# Functions
fpath=(~/.config/zsh/functions $fpath)
for func in ~/.config/zsh/functions/*(N)
do
  autoload -Uz "${func:t}"
done

# Clear screen on startup for urxvt
if [[ "$TERM" == "rxvt-unicode-256color" ]]
then
  clear
fi

# Custom FZF Ctrl+R behavior: Execute immediately
if zle -l fzf-history-widget
then
  fzf-history-widget-accept() {
    fzf-history-widget
    if [[ $? -eq 0 ]]
    then
      zle accept-line
    fi
  }
  zle -N fzf-history-widget-accept
  bindkey '^R' fzf-history-widget-accept
fi
